1 trace
.( ### 1. basic funtions ) cr
variable width
: asterisks width @ for ." *" next ;
: rectangle width ! for cr asterisks next ;
: parallelogram width ! for cr r@ spaces asterisks next ;
: triangle for cr r@ width ! asterisks next ;

3 10 rectangle cr
5 18 parallelogram cr
12 triangle cr

.( ### 2. comparisons ) cr
: weather dup 55 <
  if ." too cold!" drop
  else 85 <
    if ." about right."
    else ." too hot!"
    then then ;

90 weather cr 70 weather cr 32 weather cr
54.9999 weather cr 55.0001 weather cr
84.9999 weather cr 85.0001 weather cr

.( 3. deeper loops )
: onerow cr dup 3 .r 4 spaces 1 11 for 2dup * 4 .r 1 + next 2drop ;
: multiply cr 7 spaces 1 11 for dup 4 .r 1 + next drop 1 11 for dup onerow 1 + next drop cr ;
multiply

.( ### 4. application ) cr
variable julian variable leap
\ register leap year and days in julian calendar
: year dup
  1949 - 1461 4 */mod int 365 - julian !
  3 = if 1 else 0 then leap !
  2000 = if 0 leap ! then ;
\ calculate first day in month
: first dup 1 =
  if drop 0
  else dup 2 =
    if drop 31
    else dup 3 =
      if drop 59 leap @ +
      else 4 - 30624 1000 */ int 90 + leap @ +
      then then then ;
\ calculate days in month      
: days dup
  first swap 1 +
  first over - 1 - 1 swap
  for 2dup + 1 - julian @ + 7 mod if else cr then dup  8 u.r 1 + next
  2drop ;
\ print a row of stars  
: stars 60 for 42 emit next ;
\ display leading blanks per first day of the month
: blanks first julian @ + 7 mod 8 * spaces ;
\ print calendar header
: header
  cr stars
  cr ."      sun     mon     tue     wed     thu     fri     sat"
  cr stars cr ;
\ display month calendar based on julian date and leap year
: month header dup blanks days cr stars cr ;

2022 year 6 month

.( ### 5. benchmark 100K cycles ) cr
: 1k  999 for 34 drop next ;
: clk clock >r for 1k next clock r> - ;
: time ( n -- ) dup >r clk r> 1 + / . ."  us/cycle" ;
99 time

bye

